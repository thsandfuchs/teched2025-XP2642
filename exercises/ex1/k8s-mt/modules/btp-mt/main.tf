//
resource "random_id" "context_id" {
  byte_length = 4
  prefix      = "teched-"
}

locals {
  context_id        = random_id.context_id.hex
  subaccount_domain = lower("mt-${var.BTP_SUBACCOUNT}-${local.context_id}")
  subaccount_name   = lower("mt-${var.BTP_SUBACCOUNT}-${local.context_id}")
  region            = lower("${var.BTP_SA_REGION}")
}

resource "btp_subaccount" "create_subaccount" {
  count       = var.subaccount_id == "" ? 1 : 0

  name        = local.subaccount_name
  subdomain   = local.subaccount_domain
  region      = local.region
  //usage       = "USED_FOR_PRODUCTION"
  description = "${local.subaccount_name} is a consumer tenant subaccount"
  labels      = {
      "${var.BTP_SUBACCOUNT}"         = ["${var.BTP_SA_REGION}", "${var.BTP_CUSTOM_IDP}"],
      "${var.BTP_SUBACCOUNT}-trust"   = ["${var.BTP_CUSTOM_IDP}"],
      "${var.BTP_SUBACCOUNT}-region"  = ["${var.BTP_SA_REGION}"],
      "${var.BTP_SUBACCOUNT}-domain"  = ["${local.subaccount_domain}"],
      "${var.BTP_SUBACCOUNT}-context" = ["SAP-TechEd25-XP264"]          
  } 
}

data "btp_subaccount" "context" {
  id = var.subaccount_id == "" ? one(btp_subaccount.create_subaccount[*].id) : var.subaccount_id
}

output "btp_subaccount_labels" {
  value = jsonencode(data.btp_subaccount.context.labels)
}


# check for all the trust origins on a subaccount level: default (ids), custom and platform
#
data "btp_subaccount_trust_configurations" "all" {
  subaccount_id = data.btp_subaccount.context.id
}

output "btp_subaccount_trust_configurations_all" {
  value = data.btp_subaccount_trust_configurations.all.values
}

/*
> module.runtime_context.all_trust_origins
[
  "anuk8cmfw-platform",
  "sap.custom",
  "sap.default",
]
*/
output "all_trust_origins" {
  value = [ for trust in data.btp_subaccount_trust_configurations.all.values: trust.origin ]
}

locals {
  platform_trust_origin = [ for trust in data.btp_subaccount_trust_configurations.all.values: trust.origin if trust.type == "Platform" ]
}

output "platform_trust_origins" {
  value = local.platform_trust_origin
}


resource "btp_subaccount_role_collection_assignment" "subaccount_platform_idp_groups" {

  depends_on           = [btp_subaccount_trust_configuration.custom_idp]

  for_each             = var.subaccount_id == "" ? toset( "${var.admin_groups}" ) : toset([])
  subaccount_id        = data.btp_subaccount.context.id
  role_collection_name = "Subaccount Administrator"
  group_name           = each.value
  origin               = length(local.platform_trust_origin) != 0 ? local.platform_trust_origin[0] : "anuk8cmfw-platform"
}


# Assign users to Role Collection: subaccount emergency_admin (with sap.custom)
#
resource "btp_subaccount_role_collection_assignment" "subaccount_custom_idp_groups" {
  depends_on           = [btp_subaccount_subscription.faas_xp264_mt]

  for_each             = length(var.TECHED_MT_SUBSCRIPTION[*]) != 0 ? toset( "${var.admin_groups}" ) : toset([])
  subaccount_id        = data.btp_subaccount.context.id
  role_collection_name = "faas-xp264-049_hc-faas"
  group_name            = each.value
  origin               = one(btp_subaccount_trust_configuration.custom_idp[*].origin)
}



locals {
  custom_domain     = var.BTP_CUSTOM_IDP
  identity_provider = local.custom_domain
}

resource "btp_subaccount_trust_configuration" "custom_idp" {
  count             = var.subaccount_id == "" ? 1 : 0

  subaccount_id     = data.btp_subaccount.context.id
  identity_provider = local.identity_provider
  name              = "${local.subaccount_domain}"
  domain            = local.custom_domain

  depends_on        = [btp_subaccount.create_subaccount]

}

# custom identity provider
#
data "btp_subaccount_trust_configuration" "custom_idp" {
  count         = var.subaccount_id == "" ? 1 : 0
  depends_on    = [btp_subaccount_trust_configuration.custom_idp]

  subaccount_id = data.btp_subaccount.context.id
  origin        = one(btp_subaccount_trust_configuration.custom_idp[*].origin)
}



resource "btp_subaccount_security_settings" "sec_setting" {
  count                                    = var.subaccount_id == "" ? 1 : 0
  subaccount_id                            = data.btp_subaccount.context.id

  default_identity_provider                = "sap.custom"

/*
  access_token_validity                    = 3600
  refresh_token_validity                   = 3600
  treat_users_with_same_email_as_same_user = true
  custom_email_domains                     = ["yourdomain.test"]
  iframe_domains                           = "https://yourdomain.test"
*/  
}

# look up services offerings available on sapbtp environment in a given subaccount
data "btp_subaccount_service_offerings" "sapbtp" {
  subaccount_id = data.btp_subaccount.context.id
  environment   = "sapbtp"
}


output "sapbtp" {
  value = data.btp_subaccount_service_offerings.sapbtp.values
}


data "btp_subaccount_subscriptions" "all" {
  subaccount_id = data.btp_subaccount.context.id
}

locals {
  faas-app-xp264-049-saas = [ for i, s in data.btp_subaccount_subscriptions.all.values: s if s.app_name == "faas-app-xp264-049-saas" ]
}
output "faas-app-xp264-049-saas" {
  value = local.faas-app-xp264-049-saas
}


resource "time_sleep" "subscription_propagation" {
  count = length(var.TECHED_MT_SUBSCRIPTION[*]) != 0 ? 1 : 0

  create_duration = "20s"

  triggers = {
    # This sets up a proper dependency on the faas-app-xp264-049-saas subscription association

    app_name  = local.faas-app-xp264-049-saas[0].app_name
    plan_name = local.faas-app-xp264-049-saas[0].plan_name

  }
}


resource "btp_subaccount_subscription" "faas_xp264_mt" {
  count = length(var.TECHED_MT_SUBSCRIPTION[*]) != 0 ? 1 : 0

  depends_on    = [btp_subaccount_trust_configuration.custom_idp]

  subaccount_id = data.btp_subaccount.context.id
  app_name      = one(time_sleep.subscription_propagation[*].triggers["app_name"]) 
  plan_name     = one(time_sleep.subscription_propagation[*].triggers["plan_name"]) 

  timeouts = {
    create = "25m"
    delete = "15m"
  }  
}

/*
data "btp_subaccount_subscription" "faas_xp264_mt" {
  count = length(var.TECHED_MT_SUBSCRIPTION[*]) != 0 ? 1 : 0

  depends_on    = [btp_subaccount_subscription.faas_xp264_mt]

  subaccount_id = data.btp_subaccount.context.id
  app_name      = one(time_sleep.subscription_propagation[*].triggers["app_name"]) 
  plan_name     = "" //one(time_sleep.subscription_propagation[*].triggers["plan_name"]) 
}
*/

output "faas_xp264_mt_subscription_url" {
  value = one(btp_subaccount_subscription.faas_xp264_mt[*].subscription_url)
}

locals {

  consumer_tenant_url = "https://emea.cockpit.btp.cloud.sap/cockpit?idp=${var.BTP_CUSTOM_IDP}#/globalaccount/${var.BTP_GLOBAL_ACCOUNT}/subaccount/${data.btp_subaccount.context.id}"
  
}

output "consumer_tenant_url" {
  value = local.consumer_tenant_url
}
